apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'channel'
apply plugin: 'com.alibaba.arouter'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = "1.8"
  }

  defaultConfig {
    applicationId "com.lyy.keepassa"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 43
    versionName "2.1.7"
    multiDexEnabled true
    vectorDrawables.useSupportLibrary = true

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    // so 版本
    ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'

    // 只保留以下语言包
    resConfigs "zh-rCN", "zh-rTW", "en", "fr-rCA", "nb-rNO", "ru-rRU", "fr", "de-rDE", "pl"
  }

  signingConfigs {
    release {
      def properties = new Properties()
      def inputStream = project.rootProject.file('local.properties').newDataInputStream()
      properties.load( inputStream )

      storeFile file(properties.getProperty('storeFile'))
      storePassword properties.getProperty('storePassword')
      keyAlias properties.getProperty('keyAlias')
      keyPassword properties.getProperty('keyPassword')
      v1SigningEnabled true // v1 签名
      v2SigningEnabled true // v2 签名
    }
  }

  buildTypes {

    debug {
      debuggable = true
      // 下面两个是debug-db的参数
      resValue("string", "PORT_NUMBER", "10086") // 端口
      resValue("string", "DB_PASSWORD_keepassA.db", "stVz7QxFgzA7yMnH") // sqlcipher 加密密码
      consumerProguardFiles 'proguard-rules.pro'
    }

    release {
      //zipAlignEnabled true //开启Zipalign优化
      debuggable false
      minifyEnabled true
      shrinkResources true // 移除无用资源
      //      multiDexKeepFile file('multidex-config.txt')
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      consumerProguardFiles 'proguard-rules.pro' // wcdb 需要增加这个混淆
      signingConfig signingConfigs.release
    }
  }

  buildFeatures{
    dataBinding = true
    // for view binding :
//     viewBinding = true
  }

  testOptions {
    unitTests.includeAndroidResources = true
    unitTests.returnDefaultValues = true
  }

  externalNativeBuild {
    cmake {
      path "src/main/jni/CMakeLists.txt"
    }
  }

  lintOptions {
    checkReleaseBuilds false
    // Or, if you prefer, you can continue to check for errors in release builds,
    // but continue the build even when errors are found:
//    abortOnError false
  }

    flavorDimensions "app"
//   渠道配置 https://developer.android.google.cn/studio/build/build-variants.html?hl=zh-cn
    productFlavors {
      dev {
        dimension "app"
      }
//      fdroid{
//        dimension "app"
//      }
    }
  //
}

kapt {
  arguments {
    arg('room.schemaLocation', "$projectDir/schemas") //指定room.schemaLocation生成的文件路径，用于版本升级
    arg('AROUTER_MODULE_NAME', project.getName() + "_kpa") // arouter
    // 生成的文档路径 : build/generated/source/apt/(debug or release)/com/alibaba/android/arouter/docs/arouter-map-of-${moduleName}.json
//    arg('AROUTER_GENERATE_DOC', "enable") // arouter,release 不能打开这个
    // eventbus索引，https://greenrobot.org/eventbus/documentation/subscriber-index/
    arg('eventBusIndex', 'com.lyy.keepassa.KpaEventBusIndex') // 将会自动创建KpaEventBusIndex
  }
}

configurations {
  // 配置不同的渠道加载不同的module
  playImplementation {}
}

/**
 * 根据已有基础包重新生成多渠道包
 * 如果是自动生成渠道包，见https://github.com/Tencent/VasDolly
 */
rebuildChannel {
  //指定渠道文件
  channelFile = file("${project.getProjectDir()}/channel")
  //  baseDebugApk = 已有Debug APK
  // 已有Release APK
//  baseReleaseApk = new File("${project.buildDir}/AndResGuardFinal.apk")
  baseReleaseApk = new File("${project.buildDir}/outputs/apk/dev/release/app-dev-release.apk")
  //默认为new File(project.buildDir, "rebuildChannel/debug")
  //  debugOutputDir = Debug渠道包输出目录
  //默认为new File(project.buildDir, "rebuildChannel/release")
  // Release渠道包输出目录
  releaseOutputDir = new File("${project.buildDir}/outputs/channels")
  //快速模式：生成渠道包时不进行校验（速度可以提升10倍以上，默认为false）
  isFastMode = false
  //低内存模式（仅针对V2签名，默认为false）：只把签名块、中央目录和EOCD读取到内存，不把最大头的内容块读取到内存，在手机上合成APK时，可以使用该模式
  lowMemory = false
}

// 配置 AndResGuard 于 channel 前执行
// 这几行代码表示reBuildChannel任务是依赖于resguardRelease任务的，也就是说先执行完资源混淆后再执行多渠道打包。
afterEvaluate {
  tasks.getByName('reBuildChannel') {
    dependsOn(assembleRelease) // 暂时使用这个

    // 不要使用，1.2.21打包出来的apk，消息对话框布局错乱
//    dependsOn('resguardRelease')
  }
}

boolean isReleaseBuildType(){
  for(String s : gradle.startParameter.taskNames) {
    println("builtType: " + s)
    if (s.contains("Release") | s.contains("release") | s.contains("reBuildChannel")) {
      return true
    }
  }
  return false
}

// 替换AndroidManifest 参数
// https://developer.android.com/studio/known-issues#variant_api
android.applicationVariants.all { variant ->
  variant.outputs.all { output ->
    output.processManifest.doLast {
      // Stores the path to the maifest.
      String manifestPath = "${multiApkManifestOutputDirectory.asFile.get()}/AndroidManifest.xml"
      println("版本类型：" + isReleaseBuildType())
      def keyHash = "5qMkIKbIM5Lx7VbajX8mT3rKXpE="
      if (isReleaseBuildType()){
        keyHash = "xv3czaACtp98KvuSqP3GRLCxjrc="
      }

      // 替换one drive 配置
      def updatedContent = file(manifestPath).getText('UTF-8')
          .replaceAll("msal_package_name", "com.lyy.keepassa") // 包名
          .replaceAll("msal_signature_hash", keyHash) // 当前签名hash
      file(manifestPath).write(updatedContent, 'UTF-8')
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])


  // 测试模块
  testImplementation rootProject.ext.junit
  androidTestImplementation rootProject.ext.runner
  androidTestImplementation rootProject.ext.androidXJunit
  androidTestImplementation rootProject.ext.espresso
  androidTestImplementation rootProject.ext.androidXRules

//      debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'
  // 数据库如果加密的话使用这个
//  debugImplementation 'com.amitshekhar.android:debug-db-encrypt:1.0.6'

  // keepassA组件
  implementation 'me.laoyuyu.keepassa:KeepassIcon:0.2.6'
  implementation 'me.laoyuyu.keepassa:KeepassLib:0.2.7'
//  implementation project(path: ':KpaLib')
  implementation 'me.laoyuyu.keepassa:KPAFrame:0.2.8'
  implementation 'me.laoyuyu.keepassa:UIWidget:0.2.6'


  // kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${rootProject.ext.kotlin_version}"
  // kotlin 反射依赖
  implementation "org.jetbrains.kotlin:kotlin-reflect:${rootProject.ext.kotlin_version}"
  // Androidx 组件，包括了协程
  implementation "androidx.core:core-ktx:${rootProject.ext.ktxVersion}"
  // 协程支持
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${rootProject.ext.coroutinesVersion}"

  // =========================== android 组件 start =====================================
  implementation "androidx.appcompat:appcompat:${rootProject.ext.appcompatVersion}"
  implementation "com.google.android.material:material:${rootProject.ext.materialVersion}"
  implementation "androidx.recyclerview:recyclerview:${rootProject.ext.recyclerviewVersion}"
  implementation "androidx.multidex:multidex:${rootProject.ext.multidexVersion}"
  implementation "androidx.cardview:cardview:${rootProject.ext.cardviewVersion}"

  // lifecycle
  implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${rootProject.ext.lifecycleVersion}"
  implementation "androidx.lifecycle:lifecycle-livedata-ktx:${rootProject.ext.lifecycleVersion}"
  implementation "androidx.lifecycle:lifecycle-common-java8:${rootProject.ext.lifecycleVersion}"

  // viewpager2
  implementation "androidx.viewpager2:viewpager2:1.0.0"

  // android 设置库
  implementation "androidx.preference:preference-ktx:1.1.1"
  // workmanager
  def work_version = '2.4.0'
//  implementation "androidx.work:work-runtime:$work_version"
  implementation "androidx.work:work-runtime-ktx:$work_version"

  // room
  def room_version = '2.2.5'
  //  implementation "androidx.room:room-runtime:$room_version"
  kapt "androidx.room:room-compiler:$room_version"
  implementation "androidx.room:room-ktx:$room_version" // ktx 扩展插件

  implementation 'com.google.crypto.tink:tink-android:1.5.0'

  // android 自动填充工具
  implementation "androidx.autofill:autofill:1.1.0"

  // 下拉刷新控件
  implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

  // 约束布局
  implementation "androidx.constraintlayout:constraintlayout:${rootProject.ext.constraintlayoutVersion}"

  // 生物特征识别
  implementation "androidx.biometric:biometric:${rootProject.ext.biometricVersion}"

  // 取色板
  // https://mvnrepository.com/artifact/androidx.palette/palette
  implementation 'androidx.palette:palette-ktx:1.0.0'

  // =========================== android 组件 end =====================================

  // =========================== 三方库 start =====================================
  implementation "joda-time:joda-time:${rootProject.ext.jodaTimeVersion}"
  // 富文本
  implementation "com.zzhoujay.richtext:richtext:${rootProject.ext.richtextVerstion}"
  // 开源许可对话框
  implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'

  // 状态栏
  implementation 'com.gyf.immersionbar:immersionbar:3.0.0'
  // 大图浏览
  implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
  // eventbus
  def eventbus_version = '3.2.0'
  implementation "org.greenrobot:eventbus:$eventbus_version"
  kapt "org.greenrobot:eventbus-annotation-processor:$eventbus_version"

  // json 动画库
  implementation 'com.airbnb.android:lottie:3.5.0'

  // glide
  implementation 'com.github.bumptech.glide:glide:4.11.0'
  kapt 'com.github.bumptech.glide:compiler:4.11.0'
  //  debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.1' // 2.1 之后不需要再代码中初始化

  // dropbox
  implementation 'com.dropbox.core:dropbox-core-sdk:3.1.5'

  // webdav
  implementation 'com.thegrizzlylabs.sardine-android:sardine-android:0.6'

  //wcdb 数据库加密
  implementation 'com.tencent.wcdb:wcdb-android:1.0.8'
  implementation 'com.tencent.wcdb:room:1.0.8' // 代替 room-runtime，同时也不需要再引用 wcdb-android

  // 腾讯多渠道 VasDolly
  implementation 'com.leon.channel:helper:2.0.3'

  // XP/调试/多开/模拟器/root 判断
  implementation 'com.lahm.library:easy-protector-release:1.1.2'
  // auto-size
  implementation 'me.jessyan:autosize:1.2.1'

  // bugly
  implementation 'com.tencent.bugly:crashreport:3.4.4'
  implementation 'com.tencent.bugly:nativecrashreport:3.9.2'
  implementation "com.google.code.gson:gson:${rootProject.ext.gsonVersion}"

//  implementation "androidx.asynclayoutinflater:asynclayoutinflater:${rootProject.ext.asynclayoutinflaterVersion}"
  implementation 'com.github.tiann:FreeReflection:3.1.0'

  // onedrive
//  implementation 'com.microsoft.identity.client:msal:1.6+'
  implementation 'com.microsoft.identity.client:msal:2.+'
//  implementation 'com.microsoft.graph:microsoft-graph:1.5.+'

  implementation "com.squareup.retrofit2:retrofit:${rootProject.ext.retrofitVersion}"


  kapt 'com.alibaba:arouter-compiler:1.5.2'

  // // =========================== 三方库 end =====================================

}

// 不要使用，1.2.21打包出来的apk，消息对话框布局错乱
//apply from: 'AndResGuard.gradle'
