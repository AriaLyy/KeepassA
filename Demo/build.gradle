apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId "com.kpa.demo"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  dataBinding {
    enabled = true
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
}

// 替换AndroidManifest 参数
// https://developer.android.com/studio/known-issues#variant_api
android.applicationVariants.all { variant ->
  variant.outputs.all { output ->
    output.processManifest.doLast {
      // Stores the path to the maifest.

      String manifestPath = "${manifestOutputDirectory.asFile.get()}/AndroidManifest.xml"

      // 替换one drive 配置
      def updatedContent = file(manifestPath).getText('UTF-8')
          .replaceAll("msal_package_name", "com.kpa.demo") // 包名
          .replaceAll("msal_signature_hash", "5qMkIKbIM5Lx7VbajX8mT3rKXpE=") // 当前签名hash
      file(manifestPath).write(updatedContent, 'UTF-8')
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  // 测试模块
  testImplementation rootProject.ext.junit
  androidTestImplementation rootProject.ext.runner
  androidTestImplementation rootProject.ext.androidXJunit
  // kotlin
  implementation rootProject.ext.kotlinJdk
  implementation rootProject.ext.androidXKtx

  implementation rootProject.ext.appcompat
  implementation rootProject.ext.material
  implementation rootProject.ext.recyclerview
  implementation rootProject.ext.multidex
  implementation rootProject.ext.coroutines
  implementation project(path: ':Frame')
  implementation project(path: ':KeepassLib')
  implementation project(path: ':UIWidget')

  implementation rootProject.ext.retrofit

  // 生物识别库，指纹，人脸识别
  implementation 'androidx.biometric:biometric:1.0.1'

  // 加密工具，需要23版本
  //  implementation "androidx.security:security-crypto:1.0.0-rc01"
  implementation 'com.google.crypto.tink:tink-android:1.5.0'
  implementation 'com.microsoft.identity.client:msal:1.6+'
//  implementation 'com.microsoft.identity.client:msal:2.+'
//  implementation 'com.microsoft.graph:microsoft-graph:1.5.+'
}
